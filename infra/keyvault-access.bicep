param vaultName string
@description('App identities allowed to resolve Key Vault references.')
param readerPrincipalIds string[]

var secretsUserRole = subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '4633458b-17de-408a-b874-0445c86b69e6')

resource vault 'Microsoft.KeyVault/vaults@2023-07-01' existing = {
  name: vaultName
}

resource reader 'Microsoft.Authorization/roleAssignments@2022-04-01' = [for principalId in readerPrincipalIds: {
  scope: vault
  name: guid(vault.id, principalId, secretsUserRole)
  properties: {
    roleDefinitionId: secretsUserRole
    principalId: principalId
    principalType: 'ServicePrincipal'
  }
}]
